plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.12.2'
    id "com.gradleup.shadow" version "8.3.6"
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation project(':ulyp-common')
    implementation project(':ulyp-storage')

    implementation group: 'com.lmax', name: 'disruptor', version: '3.4.4'
    implementation group: 'org.agrona', name: 'agrona', version: '1.4.0'

    shadow group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    shadow group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    shadow group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    shadow group: 'org.slf4j', name: 'slf4j-api', version: '1.7.2'
    shadow group: 'net.bytebuddy', name: 'byte-buddy', version: '1.15.11'

    shadow group: 'org.projectlombok', name: 'lombok', version: '1.18.28'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.28'
    shadow group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.2'
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.15.11'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

if (project.hasProperty("apiBuild")) {
    dependencies {
        implementation project(':ulyp-agent-api')

        implementation group: 'io.grpc', name: 'grpc-protobuf', version: '1.27.0'
        implementation group: 'io.grpc', name: 'grpc-stub', version: '1.27.0'
        implementation group: 'io.grpc', name: 'grpc-netty', version: '1.27.0'
        implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.11.0'
        implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'

    }
}

sourceSets {
    main.java.srcDirs = ['src/main/java', '../src/main/java']
}

compileJava {
    options.debug = true
}

shadowJar {
    dependsOn 'jar'

    relocate 'org.agrona', 'shadowed.org.agrona'
    relocate 'org.intellij', 'shadowed.org.intellij'
    relocate 'org.jetbrains', 'shadowed.org.jetbrains'
    relocate 'org.slf4j', 'com.ulyp.slf4j'
    relocate 'io', 'shadowed.io'
    relocate 'com.google', 'shadowed.com.google'
    relocate 'javax.annotation', 'shadowed.javax.annotation'
    relocate 'com.lmax', 'shadowed.com.lmax'

    archiveFileName = "${rootProject.name}-agent-core-${rootProject.version}.jar"

    mergeServiceFiles()

    manifest {
        attributes(
                'Implementation-Title': "UlypAgent",
                'Implementation-Version': rootProject.version,
                'Built-By': 0xaa4eb,
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }
}

tasks.register('copyShadowJar', Copy) {
    from layout.buildDirectory.file('libs/ulyp-agent-core-' + rootProject.version + '.jar')
    into layout.buildDirectory.file('libs')
    rename { String fileName ->
        fileName = fileName.replace("-$project.version", "")
        fileName + "r"
    }
    dependsOn('shadowJar')
}

assemble {
    dependsOn('shadowJar', 'copyShadowJar')
}
