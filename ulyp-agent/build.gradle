plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.12.2'
    id "com.gradleup.shadow" version "8.3.6"
}

dependencies {
    shadow project(':ulyp-common')

    /*
     We are embedding ulyp-agent-core jar file inside ulyp-agent jar file. ulyp-agent-core jar is copied to temp folder
     and attached to bootstrap class loader on agent startup.
    */
    shadow project(':ulyp-agent-core')
    runtimeOnly files('../ulyp-agent-core/build/libs/ulyp-agent-core.jarr')

    shadow group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    shadow group: 'org.slf4j', name: 'slf4j-api', version: '1.7.2'
    shadow group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    shadow group: 'org.projectlombok', name: 'lombok', version: '1.18.28'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.28'

    testImplementation project(':ulyp-common')
    testImplementation project(':ulyp-agent-core')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
}

if (project.hasProperty("customByteBuddy")) {
    dependencies {
        implementation files('libs/byte-buddy-1.14.16-ulyp.jar')
    }
} else {
    dependencies {
        implementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.15.11'
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

sourceSets {
    main.java.srcDirs = ['src/main/java', '../src/main/java']
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['build_time'] = String.valueOf(System.currentTimeMillis())
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

compileJava {
    options.debug = true
    dependsOn(':ulyp-agent-core:shadowJar')
    dependsOn(':ulyp-agent-core:copyShadowJar')
}

shadowJar {
    dependsOn(':ulyp-agent-core:copyShadowJar')

    relocate 'org.agrona', 'shadowed.org.agrona'
    relocate 'org.slf4j', 'com.ulyp.slf4j'
    relocate 'io', 'shadowed.io'
    relocate 'com.google', 'shadowed.com.google'
    relocate 'javax.annotation', 'shadowed.javax.annotation'
    relocate 'net.bytebuddy', 'shadowed.net.bytebuddy'

    archiveFileName = "${rootProject.name}-agent-${rootProject.version}.jar"

    mergeServiceFiles()

    manifest {
        attributes(
                'Main-Class': 'com.ulyp.agent.Main',
                'Premain-Class': 'com.ulyp.agent.AgentBootstrap',
                'Agent-Class': 'com.ulyp.agent.AgentBootstrap',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Can-Set-Native-Method-Prefix': 'true',
                'Implementation-Title': "UlypAgent",
                'Implementation-Version': rootProject.version,
                'Built-By': 0xaa4eb,
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }
}

assemble {
    dependsOn('shadowJar')
}

test {
    dependsOn('shadowJar')
}
