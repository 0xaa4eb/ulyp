plugins {
    id 'java'
}

repositories {
    mavenCentral()
}


sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
        resources.srcDirs = ['src/jmh/resources']
    }
}

dependencies {
    jmhImplementation project(':ulyp-agent-core')
    jmhImplementation project(':ulyp-storage')
    jmhImplementation project(':ulyp-common')

    jmhImplementation 'org.openjdk.jmh:jmh-core:1.37'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'

    jmhImplementation group: 'org.hdrhistogram', name: 'HdrHistogram', version: '2.1.12'
    jmhImplementation group: 'org.buildobjects', name: 'jproc', version: '2.8.2'
    jmhImplementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    jmhImplementation group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    jmhImplementation group: 'org.rocksdb', name: 'rocksdbjni', version: '9.7.3'

    jmhImplementation group: 'com.hazelcast', name: 'hazelcast', version: '5.5.0'
    jmhImplementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.2'
    jmhImplementation group: 'org.apache.activemq', name: 'activemq-all', version: '5.10.2'
    jmhImplementation group: 'com.h2database', name: 'h2', version: '2.1.214'
    jmhImplementation group: 'org.springframework', name: 'spring-core', version: '4.2.6.RELEASE'
    jmhImplementation group: 'org.springframework.data', name: 'spring-data-jpa', version: '1.10.1.RELEASE'
    jmhImplementation group: 'javax.transaction', name: 'jta', version: '1.1'
    jmhImplementation group: 'org.hibernate', name: 'hibernate-entitymanager', version: '5.1.0.Final'
}

compileJava {
    dependsOn(':ulyp-agent:shadowJar')

    options.debug = true
}

compileJmhJava {
    dependsOn(':ulyp-agent-core:copyShadowJar', ':ulyp-agent-core:shadowJar')
}

def jmhArgs = []
if (project.hasProperty('jmhProf')) {
    jmhArgs.add('-prof')
    jmhArgs.add(project.findProperty('jmhProf'))
}
if (project.hasProperty('jmhGc')) {
    jmhArgs.add('-gc')
    jmhArgs.add(project.findProperty('jmhGc'))
}
if (project.hasProperty('jmhBench')) {
    jmhArgs.add(project.findProperty('jmhBench'))
}
if (project.hasProperty('jmhFormat')) {
    jmhArgs.add('-rf')
    jmhArgs.add(project.findProperty('jmhFormat'))
}
task jmh(type: JavaExec, dependsOn: jmhClasses) {
    main = 'org.openjdk.jmh.Main'
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
    dependsOn = [':ulyp-agent:shadowJar']
    args = jmhArgs
}

classes.finalizedBy(jmhClasses)